# vim: ft=tcsh
#
# Copyright 2026 Samvel Khalatyan. All rights reserved.
#
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file.

# .tcshrc - tcsh resource script, read at the beginning of each shell.

if ( ! $?prompt ) goto end

# -- PAINT LS(1).
setenv CLICOLOR 1

# -- GO SETUP
# Pick up system install
set go_base_ = "/usr/local/go125"
if ( ! $?TMUX && -d ${go_base_} ) then
  set path = (${path} ${go_base_}/bin)
endif
unset go_base_
# Pick up user binaries from `go install`.
set go_user_base_ = "$HOME"/go
if ( ! $?TMUX && -d ${go_user_base_} ) then
  set path = (${path} ${go_user_base_}/bin)
endif
unset go_user_base_

# -- C++ SETUP
set llvm_base_ = "/usr/local/llvm19"
if ( ! $?TMUX && -d ${llvm_base_} ) then
  set path = (${path} ${llvm_base_}/bin)
endif
unset llvm_base_

# -- Rust SETUP
# Pick up user binaries from CARGO_HOME
set cargo_base_ = "$HOME"/.cargo
if ( ! $?TMUX && -d ${cargo_base_} ) then
  set path = (${path} ${cargo_base_}/bin)
endif
unset cargo_base_

if ( -x "$HOME"/go/bin/go ) then
  alias go '"$HOME"/go/bin/go \!*'
endif

# -- DEVELOPMENT
# `dev' starts a tmux session with shell history scoped to the session.
alias dev '\\
  tmux has -t "=$PWD:t:s/./_/" >& /dev/null || \\
        tmux new -d -c "$PWD" -s "$PWD:t:s/./_/" && \\
        tmux setenv -t "=$PWD:t" HISTFILE "$PWD"/.history && \\
        tmux respawnw -k -t "=$PWD:t":0 ; \\
  if ($?TMUX) \\
    tmux switchc -t "=$PWD:t:s/./_/" ; \\
  if ! ($?TMUX) \\
    tmux attach -t "=$PWD:t:s/./_/" \\
    '
if ( ${?TMUX} ) then
  # In tmux(1) session.
  # Pick up histfile from tmux(1)
  set histfile_ = `tmux show-env HISTFILE >& /dev/stdout`
  if ( ! $? ) then
    set histfile_ = "${histfile_:s/HISTFILE=//}"
    if ( "$OSTYPE" == "linux" ) then
      set histfile_ = "${histfile_}.$OSTYPE"
    endif
    set histfile = "${histfile_}"
  endif
  unset histfile_
  # Pick up ssh-agent socket from tmux(1)
  alias dev-update-environment '\\
    if ( ${?TMUX} ) \\
      setenv SSH_AUTH_SOCK `tmux show-env SSH_AUTH_SOCK | cut -d'=' -f 2` \\
    '
  set postcmd_ = `alias postcmd \
    | sed -E 's/dev-update-environment//' \
    | sed -E 's/;[[:space:]]*;/;/' \
    | sed -E 's/;$//'`
  if ( ${?postcmd_} && ${#postcmd_} ) then
    alias postcmd "${postcmd_}; dev-update-environment"
  else
    alias postcmd dev-update-environment
  endif
  unset postcmd_
endif

end:

if ( -d "$HOME"/.tcshrc.d ) then
  foreach f ( "$HOME"/.tcshrc.d/*.tcsh )
    source "$f"
  end
endif
