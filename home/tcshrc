# Copyrigth (c) 2025, Samvel Khalatyan. All rights reserved.
#
# .tcshrc - tcsh resource script, read at the beginning of each shell.
#
# vim: ft=tcsh

setenv EDITOR vim
setenv PAGER less

if ( ! $?prompt ) goto end

# -- PROMPT
# force %c to represent skipped path components with `...`, csh(1)
set ellipsis
# ref: https://en.wikipedia.org/wiki/ANSI_escape_code
set esc_cyan_ = "%{\033[;36m%}"
set esc_green_ = "%{\033[;32m%}"
set esc_reset_ = "%{\033[m%}"
set prompt = "${esc_green_}%N${esc_reset_}@%m:${esc_cyan_}%c03${esc_reset_} %# "
set promptchars = "%#"
unset esc_cyan_ esc_green_ esc_reset_

# -- VI(1) PROMPT MODE
bindkey -v
bindkey '^R' i-search-back

# -- HISTORY
set history = 10000
set savehist = (10000 merge)
if ( "$OSTYPE" == "linux" ) then
  set histfile = "$HOME"/.history."$OSTYPE"
endif

# -- COMPLETION
set autolist = ambiguous
# Use history to aid expansion
set autoexpand
set autorehash

# -- PAINT LS(1).
setenv CLICOLOR 1

# -- GO SETUP
# Pick up system install
set go_base_ = "/usr/local/go125"
if ( ! $?TMUX && -d ${go_base_} ) then
  set path = (${path} ${go_base_}/bin)
endif
unset go_base_
# Pick up user binaries from `go install`.
set go_user_base_ = "$HOME"/go
if ( ! $?TMUX && -d ${go_user_base_} ) then
  set path = (${path} ${go_user_base_}/bin)
endif
unset go_user_base_

# -- C++ SETUP
set llvm_base_ = "/usr/local/llvm19"
if ( ! $?TMUX && -d ${llvm_base_} ) then
  set path = (${path} ${llvm_base_}/bin)
endif
unset llvm_base_

# -- Rust SETUP
# Pick up user binaries from CARGO_HOME
set cargo_base_ = "$HOME"/.cargo
if ( ! $?TMUX && -d ${cargo_base_} ) then
  set path = (${path} ${cargo_base_}/bin)
endif
unset cargo_base_

# -- DEVELOPMENT
# `dev' starts a tmux session with shell history scoped to the session.
alias dev '\\
  tmux has -t "=$PWD:t:s/./_/" >& /dev/null || \\
        tmux new -d -c "$PWD" -s "$PWD:t:s/./_/" && \\
        tmux setenv -t "=$PWD:t" HISTFILE "$PWD"/.history && \\
        tmux respawnw -k -t "=$PWD:t":0 ; \\
  if ($?TMUX) \\
    tmux switchc -t "=$PWD:t:s/./_/" ; \\
  if ! ($?TMUX) \\
    tmux attach -t "=$PWD:t:s/./_/" \\
    '
if ( ${?TMUX} ) then
  # In tmux(1) session.
  # Pick up histfile from tmux(1)
  set histfile_ = `tmux show-env HISTFILE >& /dev/stdout`
  if ( ! $? ) then
    set histfile_ = "${histfile_:s/HISTFILE=//}"
    if ( "$OSTYPE" == "linux" ) then
      set histfile_ = "${histfile_}.$OSTYPE"
    endif
    set histfile = "${histfile_}"
  endif
  unset histfile_
  # Pick up ssh-agent socket from tmux(1)
  alias dev-update-environment '\\
    if ( ${?TMUX} ) \\
      setenv SSH_AUTH_SOCK `tmux show-env SSH_AUTH_SOCK | cut -d'=' -f 2` \\
    '
  set postcmd_ = `alias postcmd \
    | sed -E 's/dev-update-environment//' \
    | sed -E 's/;[[:space:]]*;/;/' \
    | sed -E 's/;$//'`
  if ( ${?postcmd_} && ${#postcmd_} ) then
    alias postcmd "${postcmd_}; dev-update-environment"
  else
    alias postcmd dev-update-environment
  endif
  unset postcmd_
endif

end:
